[BITS 16] ;SET TO 16-BIT MODE.
[ORG 0x7C00] ;SET ORIGIN.

CODE_OFFSET EQU 0x8
DATA_OFFSET EQU 0x10

_START:
    CLI 
    MOV AX, 0x00
    MOV DS, AX
    MOV ES, AX
    MOV SS, AX
    MOV SP, 0x7C00 ;SET SP TO TOP OF BOOTLOADER SEGMENT.
    STI 
    
LOAD_PM:
    CLI
    LGDT[GDT_DESCRIPTOR]
    MOV EAX, CR0
    OR AL, 1 
    MOV CR0, EAX ;SET FIRST BIT OF CR0 TO 1.
    JMP CODE_OFFSET:PMODEMAIN

;GDT IMPLEMENTATION
GDT_START:
    DD 0x00000000
    DD 0x00000000

    ;CODE SEGMENT DESCRIPTOR.
    DW 0xFFFF    ;LIMIT.
    DW 0x0000    ;BASE.
    DB 0x00      ;BASE.
    DB 10011010b ;ACCESS BYTE.
    DB 11001111b ;FLAGS.
    DB 0x00	 ;BASE.

    ;DATA SEGMENT DESCRIPTOR.
    DW 0xFFFF    ;LIMIT.
    DW 0x0000    ;BASE.
    DB 0x00      ;BASE.
    DB 10010010b ;ACCESS BYTE.
    DB 11001111b ;FLAGS.
    DB 0x00	 ;BASE.

GDT_END:

GDT_DESCRIPTOR:
    DW GDT_END - GDT_START - 1 ;SIZE OF GDT-1.
    DD GDT_START ;BASE ADDRESS OF GDT.

[BITS 32] ;SET TO 32-BIT MODE.

PMODEMAIN:
    MOV AX, DATA_OFFSET
    MOV DS, AX
    MOV ES, AX
    MOV SS, AX
    MOV GS, AX
    MOV EBP, 0x9C00 ;SET BASE POINTER FOR STACK.
    MOV ESP, EBP

    IN AL, 0x92
    OR AL, 2
    OUT 0x92, AL

    JMP $

TIMES 510 - ($ - $$) DB 0

DW 0xAA55
